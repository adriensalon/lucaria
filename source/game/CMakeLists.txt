cmake_minimum_required(VERSION 3.13)
project(${LUCARIA_TARGET})

add_subdirectory("${LUCARIA_SOURCE_DIR}/client" "${LUCARIA_BINARY_DIR}/client/${LUCARIA_TARGET_PLATFORM}")

if(ANDROID)
    add_library(${LUCARIA_TARGET} SHARED ${LUCARIA_TARGET_SOURCES})
else()
    add_executable(${LUCARIA_TARGET} ${LUCARIA_TARGET_SOURCES})
endif()

set_target_properties(${LUCARIA_TARGET} PROPERTIES CXX_STANDARD 17)
target_link_libraries(${LUCARIA_TARGET} PRIVATE lucaria_client)
target_include_directories(${LUCARIA_TARGET} PRIVATE ${LUCARIA_TARGET_INCLUDES})
target_compile_definitions(${LUCARIA_TARGET} PRIVATE ${LUCARIA_TARGET_DEFINES})
target_compile_options(${LUCARIA_TARGET} PRIVATE ${LUCARIA_TARGET_BUILD_ARGS})

# emscripten options
if(EMSCRIPTEN)
    if(NOT LUCARIA_HTML_SHELL)
        set(LUCARIA_HTML_SHELL "${CMAKE_CURRENT_LIST_DIR}/web/shell.html")
    endif()

    set_target_properties(${LUCARIA_TARGET} PROPERTIES SUFFIX ".html")
    target_link_options(${LUCARIA_TARGET} PRIVATE --shell-file ${LUCARIA_HTML_SHELL})
    target_link_options(${LUCARIA_TARGET} PRIVATE -sLLD_REPORT_UNDEFINED)
    target_link_options(${LUCARIA_TARGET} PRIVATE -sALLOW_MEMORY_GROWTH=1)
    target_link_options(${LUCARIA_TARGET} PRIVATE -sINITIAL_MEMORY=671088640)
    target_link_options(${LUCARIA_TARGET} PRIVATE -sMALLOC=emmalloc)
    target_link_options(${LUCARIA_TARGET} PRIVATE -sMAX_WEBGL_VERSION=2)
    target_link_options(${LUCARIA_TARGET} PRIVATE -sUSE_WEBGL2=1)
    target_link_options(${LUCARIA_TARGET} PRIVATE -sFETCH)
    target_link_options(${LUCARIA_TARGET} PRIVATE -lopenal)
    
    if(LUCARIA_DEBUG)
        target_link_options(${LUCARIA_TARGET} PRIVATE -g)
        # target_link_options(${LUCARIA_TARGET} PRIVATE -sGL_DEBUG=1)
        target_link_options(${LUCARIA_TARGET} PRIVATE -sNO_DISABLE_EXCEPTION_CATCHING)
    else()
        target_link_options(${LUCARIA_TARGET} PRIVATE -O3)
        target_link_options(${LUCARIA_TARGET} PRIVATE -sGL_DEBUG=0)
    endif()
    
    if(LUCARIA_ASSETS_PACKAGE)
        target_link_options(${LUCARIA_TARGET} PRIVATE --preload-file ${LUCARIA_ASSETS_DIR}@/)
    endif()
endif()

# win32 options
if(WIN32 AND LUCARIA_HIDE_CONSOLE)
    target_compile_definitions(${LUCARIA_TARGET} PRIVATE -DLUCARIA_HIDE_CONSOLE=1)
    set_target_properties(${LUCARIA_TARGET} PROPERTIES WIN32_EXECUTABLE TRUE)
else()
    target_compile_definitions(${LUCARIA_TARGET} PRIVATE -DLUCARIA_HIDE_CONSOLE=0)
endif()

# lto optimization
cmake_policy(SET CMP0069 NEW)
function(enable_ipo_recursive TARGET)
    if(NOT TARGET ${TARGET})
        return()
    endif()
    get_property(already_set TARGET ${TARGET} PROPERTY _IPO_MARKER SET)
    if(already_set)
        return()
    endif()
    set_property(TARGET ${TARGET} PROPERTY _IPO_MARKER TRUE)
    set_property(TARGET ${TARGET} PROPERTY INTERPROCEDURAL_OPTIMIZATION TRUE)
    get_target_property(link_libs ${TARGET} LINK_LIBRARIES)
    if(NOT link_libs)
        return()
    endif()
    foreach(lib IN LISTS link_libs)
        if(TARGET ${lib})
            enable_ipo_recursive(${lib})
        endif()
    endforeach()
endfunction()

if(NOT LUCARIA_DEBUG)
    enable_ipo_recursive(${LUCARIA_TARGET})
endif()
