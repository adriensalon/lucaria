
if(ANDROID OR WIN32)
    add_subdirectory(${LUCARIA_EXTERNAL_DIR}/alsoft ${CMAKE_CURRENT_BINARY_DIR}/external/alsoft)
endif()
add_subdirectory(${LUCARIA_EXTERNAL_DIR}/bullet ${CMAKE_CURRENT_BINARY_DIR}/external/bullet)
add_subdirectory(${LUCARIA_EXTERNAL_DIR}/cereal ${CMAKE_CURRENT_BINARY_DIR}/external/cereal)
add_subdirectory(${LUCARIA_EXTERNAL_DIR}/entt ${CMAKE_CURRENT_BINARY_DIR}/external/entt)
if(WIN32)
    add_subdirectory(${LUCARIA_EXTERNAL_DIR}/glfw ${CMAKE_CURRENT_BINARY_DIR}/external/glfw)
endif()
add_subdirectory(${LUCARIA_EXTERNAL_DIR}/glm ${CMAKE_CURRENT_BINARY_DIR}/external/glm)
add_subdirectory(${LUCARIA_EXTERNAL_DIR}/imgui ${CMAKE_CURRENT_BINARY_DIR}/external/imgui)
add_subdirectory(${LUCARIA_EXTERNAL_DIR}/ogg ${CMAKE_CURRENT_BINARY_DIR}/external/ogg)
add_subdirectory(${LUCARIA_EXTERNAL_DIR}/ozz ${CMAKE_CURRENT_BINARY_DIR}/external/ozz)
add_subdirectory(${LUCARIA_EXTERNAL_DIR}/stb ${CMAKE_CURRENT_BINARY_DIR}/external/stb)
add_subdirectory(${LUCARIA_EXTERNAL_DIR}/vorbis ${CMAKE_CURRENT_BINARY_DIR}/external/vorbis)
add_subdirectory(${LUCARIA_EXTERNAL_DIR}/woff ${CMAKE_CURRENT_BINARY_DIR}/external/woff)

file(GLOB_RECURSE lucaria_client_sources "*.cpp")
add_library(lucaria_client STATIC ${lucaria_client_sources})
set_target_properties(lucaria_client PROPERTIES CXX_STANDARD 17)
target_compile_definitions(lucaria_client PUBLIC BT_NO_SIMD_OPERATOR_OVERLOADS) # ozz implements them already

if(ANDROID)
    set(NATIVE_APP_GLUE_DIR ${ANDROID_NDK}/sources/android/native_app_glue)
    add_library(android_native_app_glue STATIC ${NATIVE_APP_GLUE_DIR}/android_native_app_glue.c)
    target_include_directories(android_native_app_glue PUBLIC ${NATIVE_APP_GLUE_DIR})
    target_link_libraries(lucaria_client PUBLIC GLESv3 EGL android log android_native_app_glue)
endif()
if(ANDROID OR WIN32)
    # target_link_libraries(lucaria_client PUBLIC lucaria_external_alsoft)
    target_link_libraries(lucaria_client PUBLIC OpenAL)
endif()
target_link_libraries(lucaria_client PUBLIC lucaria_external_bullet)
target_link_libraries(lucaria_client PUBLIC lucaria_external_cereal)
target_link_libraries(lucaria_client PUBLIC lucaria_external_entt)
target_link_libraries(lucaria_client PUBLIC lucaria_external_glm)
if(WIN32)
    target_link_libraries(lucaria_client PUBLIC lucaria_external_glfw)
endif()
target_link_libraries(lucaria_client PUBLIC lucaria_external_imgui)
target_link_libraries(lucaria_client PUBLIC lucaria_external_ozz)
target_link_libraries(lucaria_client PUBLIC lucaria_external_stb)
target_link_libraries(lucaria_client PUBLIC lucaria_external_vorbis)
target_link_libraries(lucaria_client PUBLIC lucaria_external_woff)

target_include_directories(lucaria_client PUBLIC ${LUCARIA_INCLUDE_DIR})

message("-- Configuring engine with generator: ${CMAKE_GENERATOR}")
message("-- Configuring engine with compiler: ${CMAKE_CXX_COMPILER_ID}")
message("-- Configuring engine with config: ${LUCARIA_CONFIG}")
target_compile_definitions(lucaria_client PUBLIC -DLUCARIA_GENERATOR=${CMAKE_GENERATOR})
target_compile_definitions(lucaria_client PUBLIC -DLUCARIA_COMPILER=${CMAKE_CXX_COMPILER_ID})
target_compile_definitions(lucaria_client PUBLIC -DLUCARIA_CONFIG=${LUCARIA_CONFIG})
if(EMSCRIPTEN)
    message("-- Configuring engine with simd: SSE2 over WebAssembly 128-bit SIMD feature")
    target_compile_definitions(lucaria_client PUBLIC -DLUCARIA_SIMD=WASM128)
elseif(WIN32)
    message("-- Configuring engine with simd: SSE4.1")
    target_compile_definitions(lucaria_client PUBLIC -DLUCARIA_SIMD=SSE4.1)
else()
    message("-- Configuring engine with simd: OFF")
    target_compile_definitions(lucaria_client PUBLIC -DLUCARIA_SIMD=OFF)
endif()
if(LUCARIA_DEBUG)
    message("-- Configuring engine with exceptions: ON (Select config other than Debug to disable)")
    message("-- Configuring engine with guizmos: ON (Select config other than Debug to disable)")
    target_compile_definitions(lucaria_client PUBLIC -DLUCARIA_DEBUG=1)
    target_compile_definitions(lucaria_client PUBLIC -DLUCARIA_GUIZMO=1)
else()
    message("-- Configuring engine with exceptions: OFF (Select Debug config to enable)")
    message("-- Configuring engine with guizmos: OFF (Select Debug config to enable)")
    target_compile_definitions(lucaria_client PUBLIC -DLUCARIA_DEBUG=0)
    target_compile_definitions(lucaria_client PUBLIC -DLUCARIA_GUIZMO=0)
endif()
if(LUCARIA_ASSETS_PACKAGE)
    message("-- Configuring engine with assets packaging: ON")
    target_compile_definitions(lucaria_client PUBLIC -DLUCARIA_ASSETS_PACKAGE=1)
    target_compile_definitions(lucaria_client PUBLIC -DLUCARIA_ASSETS_FETCH=0)
else()
    message("-- Configuring engine with assets packaging: OFF")
    target_compile_definitions(lucaria_client PUBLIC -DLUCARIA_ASSETS_PACKAGE=0)
    if(ANDROID)
        message("-- Configuring engine with assets fetch: OFF (Not supported on Android)")
        target_compile_definitions(lucaria_client PUBLIC -DLUCARIA_ASSETS_FETCH=0)
    else()
        message("-- Configuring engine with assets fetch: ON")
        target_compile_definitions(lucaria_client PUBLIC -DLUCARIA_ASSETS_FETCH=1)
    endif()
endif()

if(ANDROID)
    target_compile_options(lucaria_client PRIVATE -Wno-deprecated-declarations)
endif()
